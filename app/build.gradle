apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

def localPropFile = rootProject.file("local.properties")
def localProp = new Properties()
def hasLocalProp = (localPropFile.exists())
def BUGLY_APP_KEY = ""
def UMENG_APP_KEY = ""
if (hasLocalProp) {
    localProp.load(new FileInputStream(localPropFile))
    BUGLY_APP_KEY = localProp['BUGLY_APP_KEY']
    UMENG_APP_KEY = localProp['UMENG_APP_KEY']
} else {
    BUGLY_APP_KEY = System.getenv("BUGLY_APP_KEY")
    UMENG_APP_KEY = System.getenv("UMENG_APP_KEY")
}


def releaseTime() {
    return new Date(System.currentTimeMillis()).format("yyyyMMdd")
}

android {
    compileSdkVersion 26
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "cf.android666.wanandroid"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode Integer.parseInt(releaseTime())
        versionName "1.1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {
            //腾讯bugly 设置支持的SO库架构
            abiFilters 'armeabi' //, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
    }

    lintOptions {
        //停止lint在build时检查
        abortOnError false
    }

    productFlavors {
        beta {
            buildConfigField "String", "BUGLY_APP_KEY", "\"$BUGLY_APP_KEY\""
        }
    }

    productFlavors.all {

        flavor ->
            flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name
                                           , APP_KEY          : UMENG_APP_KEY
            ]
    }

    //解决flavor Dimensions问题  http://blog.csdn.net/syif88/article/details/75009663
    flavorDimensions "versionCode"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:cardview-v7:26.1.0'
    implementation 'com.android.support:preference-v7:26.1.0'
    implementation 'com.android.support:preference-v14:26.1.0'
    implementation 'com.google.code.gson:gson:2.8.0'
    //高版本对fragment有影响
    implementation 'com.github.bumptech.glide:glide:4.0.0'
    //打印日志
    implementation 'com.orhanobut:logger:2.1.1'
    //网络相关
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.10'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.1.0'
    //解析返回数据
    implementation 'com.github.pinguo-zhouwei:MZBannerView:v2.0.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.6.0'
    //事件处理
    implementation 'org.greenrobot:eventbus:3.1.1'
    //友盟sdk
    implementation 'com.umeng.sdk:common:latest.integration'
    implementation 'com.umeng.sdk:analytics:latest.integration'
    implementation project(':mylibrary')

    //腾讯bugly
//    implementation 'com.tencent.bugly:crashreport:latest.release'
    implementation 'com.tencent.bugly:crashreport_upgrade:latest.release'
//其中latest.release指代最新版本号，也可以指定明确的版本号，例如1.2.0
    implementation 'com.tencent.bugly:nativecrashreport:latest.release'

    //内存泄漏分析
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    implementation 'com.yanzhikai:TextPathView:0.1.1'
}
